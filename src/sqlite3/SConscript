Import('*')

env = env.Clone()

# Remove warnings
import re
flags = env.subst('${CCFLAGS}')
flags = re.sub(r'-W((all)|(error))(\s|$)', '', flags)
env.Replace(CCFLAGS = flags)

env.Append(CPPDEFINES = [
  'SQLITE_OMIT_LOAD_EXTENSION',
  'SQLITE_THREADSAFE=2',
  'SQLITE_DEFAULT_CACHE_SIZE=20000',
  'SQLITE_DEFAULT_PAGE_SIZE=4096',
  'SQLITE_TEMP_STORE=2',  # Put temporary tables in memory by default
  ])

if env['PLATFORM'] != 'win32': env.CBDefine('HAVE_STDINT_H')

lib = env.Library('#/lib/sqlite3', Glob('*.c'))

hdrs = env.Install(dir = '#/include', source = 'sqlite3.h')
Depends(lib, hdrs)


Return('lib')
